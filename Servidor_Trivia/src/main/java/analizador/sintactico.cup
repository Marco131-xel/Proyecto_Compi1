package analizador;

//importaciones
import java_cup.runtime.Symbol;
import java.util.ArrayList;
import java.util.LinkedList;
import java.util.List;
import excepciones.Errores;
import datos.Usuario;

// parser
parser code
{:
    scanner s;
    parser(scanner s){this.s = s;}

    // Lista de usuarios
    public LinkedList<Usuario> usuarios = new LinkedList<>();
    public LinkedList<Errores> listaErrores = new LinkedList<>();
    
    public void syntax_error(Symbol s){
        listaErrores.add(new Errores("SINTACTICO RECUPERABLE",
                        "No se esperaba el componente "+s.value,
                        s.left,
                        s.right));
    }

    public void unrecovered_syntax_error (Symbol s){
        listaErrores.add(new Errores("SINTACTICO NO RECUPERABLE",
                        "No se esperaba el componente "+s.value,
                        s.left,
                        s.right));
    }

    public LinkedList<Usuario> getUsuarios(){
        return usuarios;
    }

    public LinkedList<Errores> getListaErrores() {
    return listaErrores;
    }

:}

action code
{:
:}

// BLOQUE SIGNOS
terminal MENOR, MAYOR, INTER, EXCLA, XSON, VERSION, IGUAL, DOSPTS;
terminal LLAVESA, LLAVESC, CORCHEA, CORCHEC, COMA, COMID;

// BLOQUE USUARIO
terminal RESO, FINSO, USENU, DATUSE;
terminal USUARIO, PASSWORD, NOMBRE, INSTITUCION;

// BASICO
terminal DECIMAL, ID;
terminal String VALOR;

nonterminal INICIO, INSTRUCCION, INSTRUCCIONES;
nonterminal CABECERA, FIN;
nonterminal USERS, LISTA_USUARIOS, USUARIO_DATOS;

start with INICIO;

INICIO ::= INSTRUCCIONES;

INSTRUCCIONES ::= INSTRUCCION
                | INSTRUCCIONES INSTRUCCION;

INSTRUCCION ::= USERS;

CABECERA ::= MENOR INTER XSON VERSION IGUAL COMID DECIMAL COMID INTER MAYOR;                

FIN ::= MENOR FINSO EXCLA MAYOR;

USERS ::= CABECERA
          MENOR EXCLA RESO DOSPTS COMID USENU COMID MAYOR
            LLAVESA COMID DATUSE COMID DOSPTS CORCHEA 
            LISTA_USUARIOS
            CORCHEC
            LLAVESC
            FIN;

LISTA_USUARIOS ::= LLAVESA USUARIO_DATOS LLAVESC
                | LLAVESA USUARIO_DATOS LLAVESC COMA LISTA_USUARIOS;

USUARIO_DATOS ::= COMID USUARIO COMID DOSPTS COMID VALOR:a COMID COMA
                  COMID PASSWORD COMID DOSPTS COMID VALOR:b COMID COMA
                  COMID NOMBRE COMID DOSPTS COMID VALOR:c COMID COMA
                  COMID INSTITUCION COMID DOSPTS COMID VALOR:d COMID
            {:
                String usuario = a;
                String password = b;
                String nombre = c;
                String institucion = d;
                
                Usuario users = new Usuario(usuario, password, nombre, institucion);   
                usuarios.add(users);
            :};